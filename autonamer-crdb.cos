**autonamer-crdb.cos
**heavily copied from Supernamer.cos
**Modified from Autonamer.agent
**Same script numbers to avoid conflicts between different namers.
enum 1 1 166
  kill targ
 next

new: simp 1 1 166 "blnk" 1 0 0
tick 1
mvto 0 0


***TIMER SCRIPT
scrp 1 1 166 9
	inst
*tick every 60 seconds or so
	tick 1200
*	enumerate across all norns
	enum 4 0 0
*		get the moniker
		sets va00 gtos 0
		sets va99 hist name va00
*		only choose ones who have no name
		doif va99 eq ""
*		get the generation
			setv va01 stoi gtos 0
			modv va01 26 
*			convert generation to string
			sets va02 vtos va01
*			add them together
			adds va02 "n"
*			get the number of entries for this section
			setv va06 rean va02
*			subtract 1
			subv va06 1
*			randomly choose index into name list
			setv va04 rand 0 va06
*			get a name particle (starts with capital letter)
			sets va05 read va02 va04
*			choose number of particles
			setv va07 rand 1 3
*			choose random paricles
			reps va07
*				get number of possible particles
				setv va06 rean "particles"
				subv va06 1
*				choose random particle
				setv va04 rand 0 va06
*				add particle to name
				adds va05 read "particles" va04
			repe
*			set the name
			hist name va00 va05
		endi
	next
endm
